\chapter{文法定义}

本章将给出与 Berry 有关的一些文法定义，我们使用\textbf{扩展的巴科斯范式}（Extended Backus Normal Form, EBNF）来定义或者表达文法。我们没有使用严格的 EBNF 语法来定义，而是做了很多简化，不过这些简化不会影响读者对文法的理解。

Berry 语言文法的 EBNF 定义如下：

\begin{lstlisting}[language=ebnf]
(* program define *)
program = block;
(* block define *)
block = {statement};
(* statement define *)
statement = class_stmt | func_stmt | var_stmt | if_stmt | while_stmt |
            for_stmt | break_stmt | return_stmt | expr_stmt | import_stmt;
if_stmt = 'if' '(' 'expr' ')' block {'elseif' '(' expr ')' block} ['else' block] 'end';
while_stmt = 'while' '(' expr ')' block 'end';
for_stmt = 'for' '(' ID ':' [expr]')' block 'end';
break_stmt = 'break' [';'];
return_stmt = 'return' [expr] [';'];
(* function define statement *)
func_stmt = 'def' ID func_body;
func_body = '(' [ID {',' ID}] ')' block 'end';
(* class define statement *)
class_stmt = 'class' ID [':' ID] class_block 'end';
class_block = {'var' ID {',' ID} | func_stmt};
import_stmt = 'import' ID ['as' ID];
(* variable define statement *)
var_stmt = 'var' ID ['=' expr] {',' ID ['=' expr]};
(* expression define *)
expr_stmt = assign_expr [';'];
assign_expr = expr ['=' expr];
expr = simple_expr | unop expr | expr binop expr;
binop = '..' | '<' | '<=' | '==' | '!=' | '>' | '>=' | '|' | '&' | '+' | '-' | '*' | '/' | '%';
unop = '+' | '-' | '!';
simple_expr =  NUM | BOOL | STRING | NIL | suffix_expr;
suffix_expr = primary_expr {call_expr | ('.' ID) | '[' expr ']'};
primary_expr = '(' expr ')' | ID | list_expr | map_expr | anon_func;
call_expr = '(' [expr {',' expr}] ')';
list_expr = '[' [expr {',' expr}] ']';
map_expr = '{' [expr ':' expr {',' expr ':' expr}] '}';
anon_func = 'def' func_body; (* anonymous function *)
\end{lstlisting}

以下是 Berry 标准库中 JSON 模块支持的 JSON 文法定义：

\begin{lstlisting}[language=ebnf,morekeywords={string,number}]
json = value;
value = object | array |
        string | number | "true" | "false" | "null";
object = '{' [ string ':' value ] { ',' string ':' value } '}';
array = '[' [json] { ',' json } ']';
\end{lstlisting}
